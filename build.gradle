buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
    }
}
plugins {
    id 'com.github.johnrengelman.shadow' version '5.2.0'
    id "dev.clojurephant.clojure" version "0.6.0-alpha.4"
}
apply plugin: 'net.minecraftforge.gradle'
// Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply plugin: 'maven'

version = "${clojure_ver}_${major}.${minor}.${patch}"
group = 'eutros.clojurelib' // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = 'clojurelib'

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.

configurations {
    shade
    implementation.extendsFrom(shade)
}

minecraft {
    mappings channel: 'snapshot', version: '20200514-1.15.1'
    runs {
        client {
            workingDirectory project.file('run')
            property 'forge.logging.console.level', 'debug'
            mods {
                clojurelib {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')
            property 'forge.logging.console.level', 'debug'
            mods {
                clojurelib {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')
            property 'forge.logging.console.level', 'debug'
            args '--mod', 'clojurelib', '--all', '--output', file('src/generated/resources/')
            mods {
                clojurelib {
                    source sourceSets.main
                }
            }
        }
    }
}

repositories {
    mavenCentral()
    maven {
        name = 'clojars'
        url = 'https://repo.clojars.org'
    }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.15.2-31.2.30'
    shade "org.clojure:clojure:${clojure_ver}"
}

// Example for how to get properties into the manifest for reading by the runtime..
jar {
    manifest {
        attributes([
                "Specification-Title"     : "clojurelib",
                "Specification-Vendor"    : "Eutros",
                "Specification-Version"   : "1",
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : "${project.version}",
                "Implementation-Vendor"   : "Eutros",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

checkClojure.enabled = false

jar {
    configurations.shade.each { dep ->
        from(project.zipTree(dep)) {
            exclude 'META-INF', 'META-INF/**'
        }
    }
    archiveClassifier.set('all')
}

clojure {
    builds {
        main {
            aotAll()
            compiler {
                setDirectLinking(true)
            }
        }
    }
}

shadowJar {
    configurations = [project.configurations.shade]
}

reobf {
    shadowJar {}
}

build.dependsOn shadowJar
build.dependsOn reobfShadowJar

processResources {
    inputs.property "version", project.version

    from(sourceSets.main.resources.srcDirs) {
        include "fabric.mod.json"
        expand "version": project.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude "fabric.mod.json"
    }
}

task cljlessJar(type: Jar) {
    from sourceSets.main.output
}

task sourceJar(type: Jar) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifact sourceJar
            artifact cljlessJar
            pom.withXml {
                // eh
                def el = asElement() as org.w3c.dom.Element;
                def dependencies = el.getElementsByTagName("dependencies").item(0)
                dependencies.getChildNodes().each {
                    for (node in it.childNodes) {
                        if (node.properties["tagName"] == "groupId") {
                            if (node.properties["textContent"] == "net.minecraftforge") {
                                dependencies.removeChild(it)
                                break
                            }
                        }
                    }
                }
            }
        }
    }

    repositories {
        maven {
            name = "Clojars"
            url = "https://clojars.org/repo"
        }
    }
}
